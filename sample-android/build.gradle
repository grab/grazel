import com.android.build.api.variant.BuildConfigField

/*
 * Copyright 2022 Grabtaxi Holdings PTE LTD (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.firebase.crashlytics"

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    defaultConfig {
        applicationId "com.grab.grazel.android.sample"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        buildConfigField("long", "SOME_LONG", "0")
        buildConfigField("int", "SOME_INT", "0")
        buildConfigField("boolean", "SOME_BOOLEAN", "false")
        buildConfigField("String", "SOME_STRING", "\"Something\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "generated_value", "This string was generated with resValue"

        manifestPlaceholders = [orientation: "portrait"]
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file("../buildsystem/debug.keystore")
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "SOME_STRING", "\"Something\"")
            resValue("string", "type", "debug")
            signingConfig signingConfigs.debug
        }
        staging {
            resValue("string", "type", "staging")
            matchingFallbacks = ["debug", "release"]
        }
    }

    flavorDimensions "service", "release"
    productFlavors {
        flavor1 {
            dimension "service"
        }
        flavor2 {
            dimension "service"
        }
        free {
            dimension "release"
            applicationIdSuffix ".free"
            resValue("string", "flavor", "free")
        }
        paid {
            dimension "release"
            applicationIdSuffix ".paid"
            resValue("string", "flavor", "paid")
        }
    }

    sourceSets {
        debug {
            res.srcDirs += "src/main/res-debug"
        }
        release {
            res.srcDirs += "src/main/res-release"
        }
        staging {
            res.srcDirs += "src/main/res-staging"
        }
    }

    buildFeatures {
        compose true
        dataBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidx.compose.get()
    }

    androidComponents {
        onVariants(selector().all()) { variant ->
            variant.buildConfigFields.put("VARIANT_NAME", new BuildConfigField("String", "\"$variant.name\"", null))
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ":sample-kotlin-library")
    implementation project(path: ":sample-android-library")
    implementation project(path: ":sample-android-flavor")
    implementation libs.androidx.appcompat
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.common
    implementation libs.androidx.activity
    implementation libs.androidx.core
    implementation libs.androidx.lifecycle.viewmodel

    implementation(libs.androidx.constraintlayout) {
        // Sample for exclude rules.
        exclude group: "androidx.appcompat", module: "appcompat"
    }

    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.foundation
    implementation libs.androidx.compose.foundation.layout
    implementation libs.androidx.activity.compose
    implementation libs.androidx.compose.material
    implementation libs.androidx.emoji2

    implementation libs.google.dagger
    kapt libs.google.dagger.compiler

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.monitor
}

apply plugin: "com.google.gms.google-services"