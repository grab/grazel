import com.android.build.api.variant.BuildConfigField

/*
 * Copyright 2022 Grabtaxi Holdings PTE LTD (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.firebase.crashlytics"

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"

    defaultConfig {
        applicationId "com.grab.grazel.android.sample"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        buildConfigField("long", "SOME_LONG", "0")
        buildConfigField("int", "SOME_INT", "0")
        buildConfigField("boolean", "SOME_BOOLEAN", "false")
        buildConfigField("String", "SOME_STRING", "\"Something\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "generated_value", "important-key"

        manifestPlaceholders = [orientation: "portrait"]
    }

    signingConfigs {
        debug {
            storeFile file("../buildsystem/debug.keystore")
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "SOME_STRING", "\"Something\"")
            resValue("string", "type", "debug")
            signingConfig signingConfigs.debug
        }
        staging {
            resValue("string", "type", "staging")
            matchingFallbacks = ["debug", "release"]
        }
    }

    flavorDimensions "service", "release"
    productFlavors {
        flavor1 {
            dimension "service"
        }
        flavor2 {
            dimension "service"
        }
        free {
            dimension "release"
            applicationIdSuffix ".free"
            resValue("string", "flavor", "free")
        }
        paid {
            dimension "release"
            applicationIdSuffix ".paid"
            resValue("string", "flavor", "paid")
        }
    }

    androidComponents {
        onVariants(selector().all()) { variant ->
            variant.buildConfigFields.put("VARIANT_NAME", new BuildConfigField("String", "\"$variant.name\"", null))
        }
    }

    sourceSets {
        debug {
            res.srcDirs += "src/main/res-debug"
        }
        release {
            res.srcDirs += "src/main/res-release"
        }
        staging {
            res.srcDirs += "src/main/res-staging"
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ":sample-kotlin-library")
    implementation project(path: ":sample-android-library")
    implementation project(path: ":sample-android-flavor")
    implementation libs.androidx.appcompat

    implementation(libs.androidx.constraintlayout) {
        // Sample for exclude rules.
        exclude group: "androidx.appcompat", module: "appcompat"
    }

    implementation libs.google.dagger
    kapt libs.google.dagger.compiler

    // Added to test per variant maven install resolution
    implementation "com.squareup.leakcanary:leakcanary-android:2.10"
    //noinspection GradleDependency
    flavor1Implementation "com.squareup.leakcanary:leakcanary-android:2.9.1"

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso.core
}

apply plugin: "com.google.gms.google-services"