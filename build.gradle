/*
 * Copyright 2022 Grabtaxi Holdings PTE LTD (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.gradle.BaseExtension
import org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile

buildscript {
    apply from: "constants.gradle"
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath libs.kotlin.gradle.plugin
        classpath libs.android.gradle.plugin
        classpath "com.grab:grazel:$versionName"
        classpath libs.google.firebase.crashlytics
        classpath libs.google.mobile.services
    }
}

plugins {
    id "idea"
}

apply plugin: "com.grab.grazel"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    group groupId
    version versionName

    configurations.configureEach {
        resolutionStrategy {
            // Force a dependency to test if Grazel picks it up correctly.
            force "com.google.dagger:dagger:2.29"
            force "com.google.dagger:dagger-compiler:2.29"
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        (project.extensions.getByName("android") as BaseExtension).compileOptions {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_11
        task.targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(KotlinJvmCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}

tasks.named("wrapper") {
    gradleVersion = libs.versions.gradle.wrapper.get()
    distributionType = Wrapper.DistributionType.ALL
}

grazel {
    android {
        dexShards = 5
        multiDexEnabled = true
        incrementalDexing = true
        variantFilter { variant ->
            variant.setIgnore(variant.buildType.name != "debug")
        }
        features {
            dataBinding = true
            dataBindingMetaData = true
        }
        ndkApiLevel = 30
    }
    dependencies {
        overrideArtifactVersions.add("androidx.constraintlayout:constraintlayout:2.1.4")
    }
    rules {
        test {
            enableTestMigration = true
            detectSourceSets = true
        }
        bazelCommon {
            gitRepository {
                commit = "fdcec09fbf9f7f700c689e816395e15c5c469bd6"
                remote = "https://github.com/grab/grab-bazel-common.git"
            }
            toolchains {
                buildifier {
                    releaseVersion = "6.0.1"
                    supportedOs = ["linux", "darwin"]
                    supportedArch = ["amd64", "arm64"]
                }
            }
        }
        googleServices {
            crashlytics {
                buildId = "042cb4d8-56f8-41a0-916a-9da28e94d1ba"
            }
            gitRepository {
                commit = "7224f55d7fafe12a72066eb1a2ad1e1526a854c4"
                remote = "https://github.com/bazelbuild/tools_android.git"
            }
        }
        mavenInstall {
            httpArchiveRepository {
                sha256 = "735602f50813eb2ea93ca3f5e43b1959bd80b213b836a07a62a29d757670b77b"
                stripPrefix = "rules_jvm_external-4.4.2"
                url = "https://github.com/bazelbuild/rules_jvm_external/archive/4.4.2.zip"
            }
            resolveTimeout = 1000
            excludeArtifactsDenyList.add("androidx.constraintlayout:constraintlayout-core")
            excludeArtifacts.add("androidx.test.espresso:espresso-contrib")
            jetifyIncludeList.add("com.android.support:cardview-v7")
            jetifyExcludeList.add("androidx.appcompat:appcompat")
            artifactPinning {
                enabled.set(true)
            }
            overrideTargetLabels.putAll(
                ["androidx.appcompat:appcompat": "@//third_party:androidx_appcompat_appcompat"]
            )
            versionConflictPolicy = "pinned"
        }
        kotlin {
            httpArchiveRepository {
                url = "https://github.com/bazelbuild/rules_kotlin/releases/download/v1.7.0-RC-3/rules_kotlin_release.tgz"
                sha256 = "f033fa36f51073eae224f18428d9493966e67c27387728b6be2ebbdae43f140e"
            }
            compiler {
                tag = "1.6.21"
                sha = "632166fed89f3f430482f5aa07f2e20b923b72ef688c8f5a7df3aa1502c6d8ba"
            }
            toolchain {
                enabled = true
                apiVersion = "1.6"
                reportUnusedDeps = "off"
                strictKotlinDeps = "off"
                abiJars = true
                multiplexWorkers = true
                languageVersion = "1.6"
                jvmTarget = "11"
            }
        }
        dagger {
            tag = "2.37"
            sha = "0f001ed38ed4ebc6f5c501c20bd35a68daf01c8dbd7541b33b7591a84fcc7b1c"
        }
    }
}

idea {
    module {
        excludeDirs += [
            file("bazel-bin"),
            file("bazel-grazel"),
            file("bazel-cache"),
            file("bazel-out"),
            file("bazel-testlogs"),
        ]
    }
}
