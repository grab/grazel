import com.grab.grazel.bazel.TestSize

/*
 * Copyright 2022 Grabtaxi Holdings PTE LTD (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    apply from: "constants.gradle"
}
plugins {
    id "com.grab.grazel.build.common"
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.kapt) apply false
    alias(libs.plugins.kotlin.parcelize) apply false
    alias(libs.plugins.google.mobile.services) apply false
    alias(libs.plugins.firebase.crashlytics) apply false
    id "idea"
    id "com.grab.grazel"
}

allprojects {
    // TODO(arun) Add support for repository in settings.gradle and then remove this
    repositories {
        google()
        mavenCentral()
    }
    group groupId
    version versionName

    configurations.configureEach {
        resolutionStrategy {
            // Force a dependency to test if Grazel picks it up correctly.
            force "com.google.dagger:dagger:2.47"
            force "com.google.dagger:dagger-compiler:2.47"
        }
    }
}

grazel {
    android {
        // dexShards = 5
        multiDexEnabled = true
        incrementalDexing = true
        variantFilter { variant ->
            variant.setIgnore(variant.buildType.name != "debug")
        }
        features {
            dataBindingMetaData = true
        }
        ndkApiLevel = 30
    }
    dependencies {
        overrideArtifactVersions.addAll(
            //"androidx.constraintlayout:constraintlayout:2.1.4",
        )
    }
    rules {
        bazelCommon {
            gitRepository {
                commit = "0962a9aaeee64ed17eb65b577060693c3b3dc244"
                remote = "https://github.com/grab/grab-bazel-common.git"
            }
            toolchains {
                buildifier {
                    releaseVersion = "6.3.3"
                    supportedOs = ["linux", "darwin"]
                    supportedArch = ["amd64", "arm64"]
                }
            }
        }
        googleServices {
            crashlytics {
                buildId = "042cb4d8-56f8-41a0-916a-9da28e94d1ba"
            }
            gitRepository {
                commit = "7224f55d7fafe12a72066eb1a2ad1e1526a854c4"
                remote = "https://github.com/bazelbuild/tools_android.git"
            }
        }
        mavenInstall {
            httpArchiveRepository {
                sha256 = "e5f83b8f2678d2b26441e5eafefb1b061826608417b8d24e5e8e15e585eab1ba"
                stripPrefix = "rules_jvm_external-6.10"
                url = String.format("https://github.com/bazelbuild/rules_jvm_external/releases/download/%s/rules_jvm_external-%s.tar.gz", "6.10", "6.10")
            }
            includeCredentials = false
            resolveTimeout = 1000
            excludeArtifactsDenyList.add("androidx.constraintlayout:constraintlayout-core")
            excludeArtifacts.add("androidx.test.espresso:espresso-contrib")

            // Force add any artifact for jetifier
            jetifyIncludeList.addAll(
                "com.android.support:cardview-v7",
                "com.android.support:support-annotations",
                "com.android.support:support-compat",
                "com.android.support:support-core-ui",
                "com.android.support:support-core-utils",
            )

            artifactPinning {
                enabled.set(true)
            }

            overrideTargetLabels.putAll(
                // Workaround for https://issuetracker.google.com/issues/285353844
                ["androidx.annotation:annotation": "@maven//:androidx_annotation_annotation_jvm"]
            )
            versionConflictPolicy = "pinned"
        }
        kotlin {
            httpArchiveRepository {
                url = "https://github.com/bazelbuild/rules_kotlin/releases/download/v1.9.6/rules_kotlin-v1.9.6.tar.gz"
                sha256 = "3b772976fec7bdcda1d84b9d39b176589424c047eb2175bed09aac630e50af43"
            }
            compiler {
                tag = "1.9.25"
                sha = "6ab72d6144e71cbbc380b770c2ad380972548c63ab6ed4c79f11c88f2967332e"
            }
            ksp {
                compiler {
                    tag = "1.9.25-1.0.20"
                    sha = "3a2d24623409ac5904c87a7e130f5b39ce9fd67ca8b44e4fe5b784a6ec102b81"
                }
            }
            toolchain {
                enabled = true
                apiVersion = "1.7"
                reportUnusedDeps = "off"
                strictKotlinDeps = "off"
                abiJars = true
                multiplexWorkers = true
                languageVersion = "1.7"
                jvmTarget = "17"
            }
            // Enable to add tags in generated tags that can be used to do classpath reduction
            // for build performance.
            enabledTransitiveReduction = false
        }
        dagger {
            tag = "2.47"
            sha = "154cdfa4f6f552a9873e2b4448f7a80415cb3427c4c771a50c6a8a8b434ffd0a"
        }
    }
    experiments {
        limitDependencyResolutionParallelism.set(true)
        minSdkVersionWorkaround.set(true)
    }
    test {
        testSizeProvider = { testData ->
            TestSize.MEDIUM
        }
    }
}

idea {
    module {
        excludeDirs += [
            file("bazel-bin"),
            file("bazel-" + projectDir.name),
            file("bazel-cache"),
            file("bazel-out"),
            file("bazel-testlogs"),
        ]
    }
}
