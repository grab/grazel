/*
 * Copyright 2022 Grabtaxi Holdings PTE LTD (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    apply from: "../constants.gradle"
    dependencies {
        classpath libs.kotlin.gradle.plugin
        classpath libs.nexus.gradle.publish.plugin
    }
}

plugins {
    id "java-gradle-plugin"
    alias(libs.plugins.kotlin.dsl)
    alias(libs.plugins.dokka.gradle.plugin)
    alias(libs.plugins.gradle.publish)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.kapt)
    id "maven-publish"
    id "idea"
}
apply from: "gradle-scripts/functional-test.gradle"
apply from: "gradle/publish-root-config.gradle"
apply from: "gradle/publishing.gradle"

group = groupId
version = versionName
description = "A Gradle plugin to automate Bazel migration for Android projects"

idea {
    module {
        sourceDirs -= file("src/functionalTest/kotlin")
        testSourceDirs += file("src/functionalTest/kotlin")
        excludeDirs += file("src/test/projects")
    }
}

pluginBundle {
    website = project.findProperty("website")
    vcsUrl = project.findProperty("website")
    description = project.description
    tags = ["bazel migration", "buildscript generation", "automation", "bazel"]
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
gradlePlugin {
    plugins {
        grazelPlugin {
            id = "com.grab.grazel"
            implementationClass = "com.grab.grazel.GrazelGradlePlugin"
            displayName = "Grazel"
        }
    }
}

dependencies {
    implementation platform("org.jetbrains.kotlin:kotlin-bom")
    implementation libs.kotlin.gradle.plugin
    compileOnly libs.android.gradle.plugin

    implementation libs.google.guava
    implementation libs.picnic

    implementation libs.google.dagger
    kapt libs.google.dagger.compiler

    implementation libs.kotlin.serialization
    implementation libs.kotlinx.coroutines.core.jvm

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation libs.google.truth
    testImplementation libs.junit
    testImplementation libs.kotlin.test
    testImplementation libs.android.gradle.plugin
    testImplementation libs.mockito.kotlin
    testImplementation gradleTestKit()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask.class).configureEach {
    compilerOptions.freeCompilerArgs.add("-Xopt-in=kotlin.ExperimentalStdlibApi")
}

tasks.withType(Test).configureEach {
    testLogging {
        showStandardStreams = true
    }
}

afterEvaluate {
    // Lifecycle task to publish both plugin marker and the plugin to central.
    tasks.register("publishToMavenCentral") {
        group = "publishing"
        dependsOn(tasks.named("publishGrazelPluginPluginMarkerMavenPublicationToSonatypeRepository"))
        dependsOn(tasks.named("publishReleasePublicationToSonatypeRepository"))
    }
}
